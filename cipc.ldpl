PROCEDURE:

SUB-PROCEDURE CIPC_DROP_MESSAGE
    PARAMETERS:
        MESSAGE     IS TEXT
        CHANNEL_ID  IS TEXT
    LOCAL DATA:
        LOCKFILE_NAME       IS TEXT
        LOCKFILE_RESULT     IS NUMBER
        LOCKFILE_COMMAND    IS TEXT
        CHANNEL_FILE        IS TEXT
    PROCEDURE:
        REPLACE "." FROM CHANNEL_ID WITH "_" IN CHANNEL_ID
        REPLACE "/" FROM CHANNEL_ID WITH "_" IN CHANNEL_ID
        IN LOCKFILE_NAME JOIN ".LDPL_CIPC_" CHANNEL_ID "_lock"
        WHILE 1 IS EQUAL TO 1 DO
            # TRY TO CREATE THE LOCKFILE
            IN LOCKFILE_COMMAND JOIN "mkdir ~/" LOCKFILE_NAME ">/dev/null 2>/dev/null"
            EXECUTE LOCKFILE_COMMAND AND STORE EXIT CODE IN LOCKFILE_RESULT
            # CHECK FOR SUCCESS
            IF LOCKFILE_RESULT IS NOT EQUAL TO 0 THEN
                # ON FAILURE, CONTINUE TRYING 
                CONTINUE
            ELSE
                # ON SUCCESS, WRITE INTO THE CHANNEL FILE
                IN CHANNEL_FILE JOIN "~/.LDPL_CIPC_" CHANNEL_ID
                APPEND MESSAGE TO FILE CHANNEL_FILE
                # DELETE THE LOCKFILE
                IN LOCKFILE_COMMAND JOIN "rm -rf ~/" LOCKFILE_NAME ">/dev/null 2>/dev/null"
                EXECUTE LOCKFILE_COMMAND
                BREAK
            END IF
        REPEAT
END SUB-PROCEDURE
CREATE STATEMENT "WRITE $ TO CHANNEL $" EXECUTING CIPC_DROP_MESSAGE

SUB-PROCEDURE CIPC_READ_MESSAGE
    PARAMETERS:
        CHANNEL_ID  IS TEXT
        RESULT      IS TEXT
    LOCAL DATA:
        LOCKFILE_NAME       IS TEXT
        LOCKFILE_RESULT     IS NUMBER
        LOCKFILE_COMMAND    IS TEXT
        CHANNEL_FILE        IS TEXT
    PROCEDURE:
        REPLACE "." FROM CHANNEL_ID WITH "_" IN CHANNEL_ID
        REPLACE "/" FROM CHANNEL_ID WITH "_" IN CHANNEL_ID
        IN LOCKFILE_NAME JOIN ".LDPL_CIPC_" CHANNEL_ID "_lock"
        WHILE 1 IS EQUAL TO 1 DO
            # TRY TO CREATE THE LOCKFILE
            IN LOCKFILE_COMMAND JOIN "mkdir ~/" LOCKFILE_NAME ">/dev/null 2>/dev/null"
            EXECUTE LOCKFILE_COMMAND AND STORE EXIT CODE IN LOCKFILE_RESULT
            # CHECK FOR SUCCESS
            IF LOCKFILE_RESULT IS NOT EQUAL TO 0 THEN
                # ON FAILURE, CONTINUE TRYING 
                CONTINUE
            ELSE
                # ON SUCCESS, READ FROM THE CHANNEL FILE
                IN CHANNEL_FILE JOIN "~/.LDPL_CIPC_" CHANNEL_ID
                LOAD FILE CHANNEL_FILE IN RESULT
                # DELETE THE CHANNEL FILE
                IN LOCKFILE_COMMAND JOIN "rm ~/" CHANNEL_FILE ">/dev/null 2>/dev/null"
                EXECUTE LOCKFILE_COMMAND
                # DELETE THE LOCKFILE
                IN LOCKFILE_COMMAND JOIN "rm -rf ~/" LOCKFILE_NAME ">/dev/null 2>/dev/null"
                EXECUTE LOCKFILE_COMMAND
                BREAK
            END IF
        REPEAT
END SUB-PROCEDURE
CREATE STATEMENT "READ CHANNEL $ IN $" EXECUTING CIPC_READ_MESSAGE